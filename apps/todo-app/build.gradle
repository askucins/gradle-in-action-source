plugins {
    id 'groovy'
    id 'application'
    id 'idea'
    id 'com.github.johnrengelman.shadow' version '4.0.4'
}

version = 0.1

repositories {
    jcenter()
}

dependencies {
    compile "org.codehaus.groovy:groovy-all:2.5.6"
    testCompile("org.spockframework:spock-core:1.3-RC1-groovy-2.5") {
        exclude group: "org.codehaus.groovy"
    }
    // To support Spock mocks
    testCompile group: 'cglib', name: 'cglib-nodep', version: '3.2.6'
    testCompile group: 'org.objenesis', name: 'objenesis', version: '2.6'
}

/* ******************************************************************************** */
jar {
    manifest {
        attributes(
                'Main-Class': 'com.manning.gia.todo.ToDoApp'
        )
    }
}

/* ******************************************************************************** */
// Run application with JavaExec task
task runWithJavaExec(type: JavaExec) {
    dependsOn classes
    classpath configurations.runtime, sourceSets.main.output.classesDirs
    main = 'com.manning.gia.todo.ToDoApp'
    standardInput = System.in
}

/* ******************************************************************************** */
// plugin application (distribution plus 'run' task)
mainClassName = 'com.manning.gia.todo.ToDoApp'
run {
    standardInput = System.in
}

/* ******************************************************************************** */
tasks.withType(Test) {
    testLogging {
        events 'passed', 'failed'
        exceptionFormat 'full'
    }
}


